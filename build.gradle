plugins {
  id 'org.springframework.boot' version '2.5.3'
  id 'com.gorylenko.gradle-git-properties' version '2.3.1'
  id 'com.palantir.git-version' version '0.12.3'
  id 'checkstyle'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group 'izanagi'
version version

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
  implementation 'org.springframework.boot:spring-boot-devtools'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'

  implementation group: 'org.modelmapper', name: 'modelmapper', version: '2.4.4'
  implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

  // RDBMS
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.23'
  implementation group: 'org.hsqldb', name: 'hsqldb', version: '2.6.0'
  implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.26'
  //implementation group: 'org.flywaydb', name: 'flyway-core', version: '7.11.3'

  // Unix Domain Socket
  implementation group: 'com.kohlschutter.junixsocket', name: 'junixsocket-common', version: '2.3.4'
  implementation group: 'com.kohlschutter.junixsocket', name: 'junixsocket-native-common', version: '2.3.4'

  // OpenAPI
  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.5.9'

  // Micrometer Registry Prometheus
  implementation group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: '1.7.2'

  // Lombok
  compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
}

bootJar {
  launchScript()
}

bootRun {
  if (project.hasProperty('jvmArgs')) {
    jvmArgs = (project.jvmArgs.split("\\s+") as List)
  }
}

test {
  useJUnitPlatform()
  if (project.hasProperty("jvmArgs")) {
    jvmArgs = (project.jvmArgs.split("\\s+") as List)
  }
}

configurations.all {
  resolutionStrategy {
    cacheChangingModulesFor 0, 'seconds'
    cacheDynamicVersionsFor 0, 'seconds'
  }
}

checkstyle {
  toolVersion '8.44'
}

springBoot {
  buildInfo()
}

task showVersion {
  doFirst {
    println "${version}"
  }
}

processResources {
  filesMatching("**/application.yml") {
    expand(project.properties)
  }
}
